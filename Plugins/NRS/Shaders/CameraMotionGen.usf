#include "/Engine/Private/Common.ush"
#include "/Engine/Private/ScreenPass.ush"

Texture2D InputDepth;
Texture2D InputVelocity;
RWTexture2D<float2> OutputTexture;

float2 ComputeStaticVelocity(float2 ScreenPos, float DeviceZ)
{
	float3 PosN = float3(ScreenPos, DeviceZ);
	float4 ThisClip = float4(PosN, 1);
	float4 PrevClip = mul(ThisClip, View.ClipToPrevClip); 
	float2 PrevScreen = PrevClip.xy / PrevClip.w;
	return PosN.xy - PrevScreen;
}

[numthreads(THREADGROUP_SIZEX, THREADGROUP_SIZEY, THREADGROUP_SIZEZ)]
void MainCS(uint3 LocalThreadId : SV_GroupThreadID, uint3 WorkGroupId : SV_GroupID, uint3 DispatchThreadId : SV_DispatchThreadID)
{
	uint Width = View.ViewSizeAndInvSize.x;
	uint Height = View.ViewSizeAndInvSize.y;

	if (Width > DispatchThreadId.x && Height > DispatchThreadId.y)
	{
		float Depth = InputDepth[DispatchThreadId.xy + View.ViewRectMin.xy].x;
		float2 ViewportUV = (DispatchThreadId.xy + 0.5) * View.ViewSizeAndInvSize.zw;
		float2 ScreenPos = ViewportUVToScreenPos(ViewportUV);

		float2 Velocity = ComputeStaticVelocity(ScreenPos, Depth).xy;

		uint2 OutPixel = DispatchThreadId.xy + View.ViewRectMin.xy;
		// to SNORM: [-1, 1]
		OutputTexture[OutPixel] = Velocity * float2(0.5, 0.5);
	}
}
