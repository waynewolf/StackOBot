#include "/Engine/Private/Common.ush"
#include "/Engine/Private/ScreenPass.ush"

Texture2D InputMotion;
SamplerState InputMotionSampler;
Texture2D InputColor;
SamplerState InputColorSampler;

float DistanceToSegment(float2 p, float2 a, float2 b)
{
	float2 ab = b - a;
	float t = dot(p - a, ab) / max(dot(ab, ab), 1e-6);
	t = saturate(t);
	float2 c = a + ab * t;
	return length(p - c);
}

float4 MainPS(FScreenVertexOutput In) : SV_Target0
{
	float2 PixelPos = In.UV * View.ViewSizeAndInvSize.xy + View.ViewRectMin.xy;
	float2 CellBase = floor(PixelPos / 16.0f) * 16.0f;

	float2 MotionSum = 0.0f;
	const int SampleCount = 4;
	const float Step = 16.0f / SampleCount;
	for (int y = 0; y < SampleCount; ++y)
	{
		for (int x = 0; x < SampleCount; ++x)
		{
			float2 SamplePos = CellBase + float2((x + 0.5f) * Step, (y + 0.5f) * Step);
			float2 SampleUV = (SamplePos - View.ViewRectMin.xy) * View.ViewSizeAndInvSize.zw;
			MotionSum += InputMotion.SampleLevel(InputMotionSampler, SampleUV, 0).xy;
		}
	}
	float2 AvgMotion = MotionSum / float(SampleCount * SampleCount);
	float MotionMag = length(AvgMotion);

	float2 Dir = (MotionMag > 1e-4f) ? (AvgMotion / MotionMag) : float2(0.0f, -1.0f);
	float ArrowLen = lerp(4.0f, 12.0f, saturate(sqrt(MotionMag) * 4.0f));

	float2 CellPos = PixelPos - CellBase;
	float2 Center = float2(8.0f, 8.0f);

	float2 Tip = Center + Dir * ArrowLen;
	float2 Tail = Center - Dir * 1.5f;

	float LineDist = DistanceToSegment(CellPos, Tail, Tip);
	float LineMask = 1.0f - smoothstep(0.5f, 1.0f, LineDist);

	float2 Left = Tip - Dir * 1.5f + float2(-Dir.y, Dir.x) * 2.0f;
	float2 Right = Tip - Dir * 1.5f + float2(Dir.y, -Dir.x) * 2.0f;
	float HeadMask = 1.0f - smoothstep(0.5f, 1.0f, min(DistanceToSegment(CellPos, Tip, Left), DistanceToSegment(CellPos, Tip, Right)));

	float ArrowMask = saturate(LineMask + HeadMask) * saturate(MotionMag * 10.0f);
	float3 ArrowColor = float3(1.0f, 0.0f, 0.0f);
	float3 SceneColor = InputColor.SampleLevel(InputColorSampler, In.UV, 0).rgb;
	float3 OutColor = lerp(SceneColor, ArrowColor, ArrowMask);

	return float4(OutColor, 1.0f);
}
